

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, LBRACE, RBRACE, CONST, ASSIGN, SEMI, COMMA, OR;
terminal LBRACKET, RBRACKET, STATIC, LPAREN, RPAREN, VOID, COLON, IF, ELSE, BREAK, CONTINUE, RETURN;
terminal READ, PRINT, INCREMENT, DECREMENT, AND, PLUS, MINUS, NEW, DOT, ASTERISK, FORWARD_SLASH, PERCENT;
terminal EQUAL, NOT_EQUAL, GREATER_THAN, GREATER_THAN_OR_EQUAL, LESS_THAN, LESS_THAN_OR_EQUAL;
terminal Integer NUMBER;
terminal String IDENT;
terminal Character CHAR;
terminal Boolean BOOL;

nonterminal VarOrConstDeclList, MethodDeclList, VarOrConstDecl, VarDecl, Const;
nonterminal BracketsOpt, VarDeclSuffix, VarDeclList, StaticInitializer, StatementList, TypeOrVoid, FormParsOpt;
nonterminal FormPars, FormParsList, Statement, DesignatorStatement, Condition, Assignop, ActPars;
nonterminal CommaExprList, CondTerm, OrCondTermList, CondFact, AndCondFactList, Relop, Addop;
nonterminal Mulop, Label, Empty;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName, Program, MethodDecl, MethodTypeName, DesignName, Designator, VarDeclElems, ConstDeclSuffix, ConstDecl;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Factor, Expr, AddopTermList, MulopFactorList;

Program ::= (Program) PROG ProgName:progName VarOrConstDeclList LBRACE MethodDecl RBRACE;

ProgName ::= (ProgName) IDENT:progName;

VarOrConstDecl ::= (VarOrConstDeclVar) VarDecl
				| (VarOrConstDeclConst) ConstDecl
				;

VarOrConstDeclList ::= (VarOrConstDeclList1) VarOrConstDeclList VarOrConstDecl
					| (NoVarOrConstDeclList) /* epsilon */
					;

ConstDecl ::= (ConstDecl) CONST Type ConstDeclSuffix SEMI;

ConstDeclSuffix ::= (ConstDeclSuffixNum1) ConstDeclSuffix COMMA IDENT:constName ASSIGN NUMBER:literal
				| (ConstDeclSuffixNum2) IDENT:constName ASSIGN NUMBER:literal
				| (ConstDeclSuffixChar1) ConstDeclSuffix COMMA IDENT:constName ASSIGN CHAR:ch
				| (ConstDeclSuffixChar2) IDENT:constName ASSIGN CHAR:ch
				| (ConstDeclSuffixBool1) ConstDeclSuffix COMMA IDENT:constName ASSIGN BOOL:bo
				| (ConstDeclSuffixBool2) IDENT:constName ASSIGN BOOL:bo
				;

Const ::= (ConstNum) NUMBER
		| (ConstChar) CHAR
		| (ConstBool) BOOL
		;
		
VarDeclList ::= (VarDeclList1) VarDeclList VarDecl
			| (NoVarDeclList) /* epsilon */
			;

VarDecl ::= (VarDecl1) Type VarDeclElems SEMI
		| (VarDeclErrorComma) COMMA error:l {: parser.report_error("Izvrsen oporavak pri deklaraciji globalnih promjenjivih do , u liniji " + lleft, null); :}
		| (VarDeclErrorSemi) SEMI error:l {: parser.report_error("Izvrsen oporavak pri deklaraciji globalnih promjenjivih do ; u liniji " + lleft, null); :}
		;

BracketsOpt ::= (BracketsOptArray) LBRACKET RBRACKET
			| (BracketsOptMatrix) LBRACKET RBRACKET LBRACKET RBRACKET
 			| (NoBracketsOpt) /* epsilon */
			;
			
VarDeclElems ::= (VarDeclElems1Multiple) VarDeclElems COMMA IDENT:varName
		| (VarDeclElemsArrayMultiple) VarDeclElems COMMA IDENT:varName LBRACKET RBRACKET
		| (VarDeclElemsMatrixMultiple) VarDeclElems COMMA IDENT:varName LBRACKET RBRACKET Empty LBRACKET RBRACKET
		| (VarDeclElems1) IDENT:varName
		| (VarDeclElemsArray) IDENT:varName LBRACKET RBRACKET
		| (VarDeclElemsMatrix) IDENT:varName LBRACKET RBRACKET Empty LBRACKET RBRACKET
		;
		
		
VarDeclSuffix ::= (VarDeclSuffix1) VarDeclSuffix COMMA Type IDENT BracketsOpt
				| (NoVarDeclSuffix) /* epsilon */
				;

StaticInitializer ::= (StaticInitializer1) STATIC LBRACE StatementList RBRACE;

MethodDecl ::= (MethodDecl) MethodTypeName LPAREN RPAREN VarDeclList LBRACE StatementList RBRACE;

MethodTypeName ::= (MethodTypeNameVoid) VOID IDENT:methName;

MethodDeclList ::= (MethodDeclList1) MethodDeclList MethodDecl
				| (NoMethodDeclList) /* epsilon */
				;

FormParsOpt ::= (FormParsOpt1) FormPars
			| (NoFormParsOpt) /* epsilon */
			;

TypeOrVoid ::= (TypeOrVoidType) Type
			| (TypeOrVoidVoid) VOID
			;

FormPars ::= (FormPars1) Type IDENT FormParsList
			| (FormPars2) Type IDENT LBRACKET RBRACKET FormParsList
			;

FormParsList ::= (FormParsList1) FormParsList COMMA Type IDENT
				| (FormParsList2) FormParsList COMMA Type IDENT LBRACKET RBRACKET
				| (NoFormParsList) /* epsilon */
				;

Type ::= (Type) IDENT:typeName
		;

Statement ::= (Statement1) DesignatorStatement SEMI
			| (StatementIf) IF LPAREN Condition RPAREN Statement
			| (StatementBreak) BREAK SEMI
			| (StatementContinue) CONTINUE SEMI
			| (StatementReturnExpr) RETURN Expr SEMI
			| (StatementReturn) RETURN SEMI
			| (StatementRead) READ LPAREN Designator RPAREN SEMI
			| (StatementPrintExpr) PRINT LPAREN Expr RPAREN SEMI
			| (StatementPrintExprWithNum) PRINT LPAREN Expr COMMA NUMBER:num RPAREN SEMI
			| (Statement2) LBRACE StatementList RBRACE
			;

StatementList ::= (StatementList1) StatementList Statement
				| (NoStatementList) /* epsilon */
				;

DesignatorStatement ::= (DesignatorStatement1) Designator Assignop Expr
					| (DesignatorStatement2) Designator LPAREN ActPars RPAREN
					| (DesignatorStatementInc) Designator INCREMENT
					| (DesignatorStatementDec) Designator DECREMENT
					| (DesignatorStatementError) SEMI error:l {: parser.report_error("Izvrsen oporavak pri designator statement-u do ; u liniji " + lleft, null); :}
					;

ActPars ::= (ActPars1) Expr CommaExprList
		| (NoActPars) /* epsilon */
		;

CommaExprList ::= (CommaExprList1) CommaExprList COMMA Expr
				| (NoCommaExprList) /* epsilon */
				;

Condition ::= (Condition1) CondTerm OrCondTermList;

OrCondTermList ::= (OrCondTermList1) OrCondTermList OR CondTerm
				| (NoOrCondTermList) /* epsilon */
				;

CondTerm ::= (CondTerm1) CondFact AndCondFactList;

AndCondFactList ::= (AndCondFactList1) AndCondFactList AND CondFact
				| (NoAndCondFactList) /* epsilon */
				;

CondFact ::= (CondFact1) Expr
			| (CondFact2) Expr Relop Expr
			;

Expr ::= (ExprPlus) Term
		| (ExprMinus) MINUS Term
		| (ExprMultiple) Expr Addop Term
		;

AddopTermList ::= (AddopTermList1) AddopTermList Addop
				| (NoAddopTermList) /* epsilon */
				;

Term ::= (Term1) Factor
		| (Term2) Term Mulop Factor
		;

MulopFactorList ::= (MulopFactorList1) MulopFactorList Mulop Factor
				| (NoMulopFactorList) /* epsilon */
				;

Factor ::= (Factor1) Designator
		| (Factor2) Designator LPAREN ActPars RPAREN
		| (FactorNum) NUMBER
		| (FactorChar) CHAR
		| (FactorBool) BOOL
		| (Factor3) NEW Type LBRACKET Expr RBRACKET
		| (Factor4) NEW Type LBRACKET Expr RBRACKET Empty LBRACKET Expr RBRACKET
		| (Factor5) NEW Type LPAREN ActPars RPAREN
		| (Factor6) LPAREN Expr RPAREN
		;
		
Empty ::= (Empty);

Designator ::= (Designator1) IDENT:desName
			| (DesignatorArray) DesignName:desName LBRACKET Expr RBRACKET
			| (DesignatorMatrix) DesignName:desName LBRACKET Expr:exa RBRACKET Empty LBRACKET Expr:exb RBRACKET
            ;
DesignName ::= (DesignName) IDENT:name;
Label ::= (LabelClass) IDENT;

Assignop ::= (Assignop) ASSIGN;

Relop ::= (Equalop) EQUAL
		| (NotEqualop) NOT_EQUAL
		| (GreaterThanop) GREATER_THAN
		| (GreaterThanOrEqualop) GREATER_THAN_OR_EQUAL
		| (LessThanop) LESS_THAN
		| (LessThanOrEqualop) LESS_THAN_OR_EQUAL
		;

Addop ::= (AddopPlus) PLUS
		| (Subop) MINUS
		;
		
Mulop ::= (Mulop1) ASTERISK
		| (Divop) FORWARD_SLASH
		| (Modop) PERCENT
		;

